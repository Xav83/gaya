(* This file contains functions for working on sequences. *)

include "base"
include "functions"

(*
  Call the provided function for each element in the sequence, discarding
  the results.
  @param s <sequence> A sequence.
  @param f <function> A function to call on every element in the sequence.
 *)
seq.foreach :: { s: Sequence, f: Function => do
    let xs = tosequence(s), next = seq.next(xs) in perform
    while next /= unit : next <- seq.next(xs)
      f(next).
    end
  end
}

(* 
  Reduce the provided sequence according to the specified accumulator function,
  using the given initial element. Return the initial element if the sequence
  was empty.
  @param s <sequence> A sequence.
  @param init <object> The initial element.
  @param f <function> The accumulator function.
 *)
seq.reduce :: { s: Sequence, init, f: Function => do
    let acc = init, xs = tosequence(s), next = seq.next(xs) in do
      while next /= unit : next <- seq.next(xs)
        acc <- f(acc, next)
      end

      acc
    end
  end
}

(*
  Return true if any element in the provided sequence
  satisfies the given predicate. 
*)
seq.any :: { xs: Sequence, pred => do
  let xs = tosequence(xs), 
      x = seq.next(xs),
      found = false, 
   in do
      while x /= unit and not found 
        found <- pred(x)
        x <- seq.next(xs)
      end

      found
    end
  end
}

(*
  Returns whether all the elements in the provided sequence satifsy
  the given predicate.
*)
seq.all :: { xs: Sequence, pred => do
    let xs = tosequence(xs),
        x = seq.next(xs),
        success = true
     in do
        while x /= unit and success : x <- seq.next(xs)
          success <- pred(x)
        end

        success
    end
  end
}

(*
  Return the greatest element in a sequence according to the '>' operator.
  If the sequence is empty, unit is returned.
  @param xs <sequence> The sequence.
*)
seq.max :: { xs: Sequence => do
  let xs = tosequence(xs),
      next = seq.next(xs),
      max = next
   in do
        while next /= unit : next <- seq.next(xs)
          max <- fn.max(max, next)
        end

        max
      end
  end
}

(*
  Return the least element in a sequence according to the '<' operator.
  If the sequence is empty, unit is returned.
  @param xs <sequence> The sequence.
*)
seq.min :: { xs: Sequence => do
  let xs = tosequence(xs),
      next = seq.next(xs),
      min = next
   in do
        while next /= unit : next <- seq.next(xs)
          min <- fn.min(min, next)
        end

        min
      end
  end
}

(*
  Return an array containing the least and greatest elements in a sequence
  according to the '<' and '>' operators respectively. 
  If the sequence is empty, unit is returned.
  @param xs <sequence> The sequence.
*)
seq.minmax :: { xs: Sequence => do
  let xs = tosequence(xs),
      next = seq.next(xs),
      min = next,
      max = next
   in do
        while next /= unit : next <- seq.next(xs)
          max <- fn.max(max, next)
          min <- fn.min(min, next)
        end

        cases (min, max)
          given (unit, unit) => unit
          given a            => a
        end
      end
  end
}

(*
  Return a new sequence that maps the given function over the elements of the
  provided sequence. 
*)
seq.map :: { s: Sequence, func: Function => do
    let xs = tosequence(s) in seq.make { =>
      let next = seq.next(xs) in
      cases 
        given next /= unit => func(next)
      end
    }
  end
}

(*
  Like map, but the callback function additionally receives the element index
  as its first argument.
  @param xs <sequence> The sequence.
  @param func <function> The transformation function.
*)
seq.mapWithIndex :: { xs: Sequence, func: Function => do
    let xs = tosequence(xs), i = 0 in
    seq.make { =>
      let next = seq.next(xs) in
      cases 
        given next /= unit =>
        let result = func(i, next) in do
          i <- i + 1
          result
        end
      end
    }
  end
}

(*
  Return a new sequence that filters out elements for which the provided
  predicate function returns false.
*)
seq.filter :: { xs: Sequence, func: Function =>
  let xs = tosequence(xs) in seq.make { => 
    let x = seq.next(xs) in do
      while x /= unit and not func(x)
        x <- seq.next(xs)
      end

      x
    end
  }
}

(*
  Return a new sequence that applies the provided function to the elements of
  the given sequence in a pairwise manner.
*)
seq.zip :: { xs: Sequence, ys: Sequence, func: Function => do
    let xs = tosequence(xs), ys = tosequence(ys) in seq.make { =>
      let x = seq.next(xs), y = seq.next(ys) in
      cases 
        given x /= unit and y /= unit => func(x, y)
      end
    }
  end
}

(*
  Return a sequence that takes elements from the input sequences as long as
  they satisfy the given predicate.
*)
seq.takewhile :: { xs: Sequence, pred: Function => do
    let xs = tosequence(xs) in seq.make { =>
      let x = seq.next(xs) in
      cases
        given pred(x) => x
      end
    }
  end
}

(* Return a sequence that takes the first n elements of the provided one. *)
seq.take :: { xs: Sequence, n: Number => do
    let xs = tosequence(xs), i = 0 in seq.make { =>
      cases
        given i < n => do
          i <- i + 1
          seq.next(xs)
        end
      end
    }
  end
}

(*
  Return a sequence that drops elements from the input sequence as long as they
  satisfy the given predicate.
*)
seq.dropwhile :: { xs: Sequence, pred: Function => do
    let xs = tosequence(xs), predicateFailed = false in seq.make { =>
      let x = seq.next(xs) in do
        while x /= unit and not predicateFailed
          cases
            given pred(x) => perform x <- seq.next(xs) 
            otherwise     => perform predicateFailed <- true
          end.
        end

        x
      end
    }
  end
}

(* Return a new sequence that drops the first n elements of the provided one. *)
seq.drop :: { xs: Sequence, n: Number =>
  let xs = tosequence(xs) in
  let i = 0 in
  seq.make { => 
    do
      while i < n : i <- i + 1
        seq.next(xs).
      end
      
      seq.next(xs)
    end
  }
}

(*
  Create a sequence that yields the numbers from start inclusive to end
  exclusive.
*)
seq.range :: { start: Number, finish: Number => do
    let i = start in seq.make { =>
      cases
        given i < finish =>
          let previous = i in do
            i <- i + 1
            previous
        end
      end
    }
  end
}

(* 
  Return a sequence that yields 2 element arrays where the first element is the
  index of the second element from the provided sequence.
*)
seq.enumerate :: { xs: Sequence => do
    let xs = tosequence(xs), i = 0 in seq.make { =>
      let x = seq.next(xs), pair = (i, x) in 
      cases
        given x /= unit => do
          i <- i + 1
          pair
        end
      end
    }
  end
}

(* Return an array with the elements of the provided sequence. *)
seq.toarray :: { xs: Sequence => do
    let xs = tosequence(xs), x = seq.next(xs), ary = () in do
      while x /= unit : x <- seq.next(xs)
        array.push(ary, x).
      end

      ary
    end
  end
}

(*
  Return a string with the elements of the provided sequence. 

  `tostring` is called on every element of the sequence to transform it to a
  string.
*)
seq.tostring :: { xs: Sequence => do
    let xs = tosequence(xs), x = seq.next(xs), s = "" in do
      while x /= unit : x <- seq.next(xs)
        s <- x |> tostring(_) |> string.concat(s, _)
      end

      s
    end
  end
}

(*
  Return the sum of the numbers in the provided sequence.

  This function consumes the provided sequence.
  @param xs <sequence> The sequence.
*)
seq.sum :: { xs => seq.reduce(xs, 0, fn.add) }

(*
  Return the number of elements in a sequence.

  This function consumes the provided sequence.
  @param xs <sequence> The sequence.
*)
seq.count :: { xs: Sequence =>
  do
    let count = 0 in
    let xs = tosequence(xs) in
    do
      while seq.next(xs) /= unit
        count <- count + 1
      end
      count
    end
  end
}

(*
  Return the first element in the sequence, or unit if there are none.
  @param xs <sequence> The sequence.
*)
seq.first :: { xs: Sequence =>
  xs
  |> tosequence(_)
  |> seq.next(_)
}

(*
  Returns a sequence over the initial elements of the provided sequence,
  shortest first.
  @param xs <Sequence> The sequence.
*)
seq.inits :: { xs: Sequence => 
  let count = xs |> seq.copy(_) |> seq.count(_) in
  let i = 0 in
  seq.make { =>
    cases
      given i <= count => 
        let seq = xs |> seq.copy(_) |> seq.take(_, i) in
        do
          i <- i + 1
          seq
        end
    end
  }
}

(*
  Returns a sequence over the final segments of the provided sequence,
  longest first.

  NOTE: Copying the sequence here makes it very, very slow.
  FIXME: Find a way to make this faster.

  @param xs <Sequence> The sequence.
*)
seq.tails :: { xs: Sequence =>
  let count = xs |> seq.copy(_) |> seq.count(_) in
  let i = 0 in
  seq.make { =>
    cases
      given i <= count =>
        let seq = xs |> seq.copy(_) |> seq.drop(_, i) in
        do
          i <- i + 1
          seq
        end
      end
  }
}

(*
  Creates a new sequence that flattens one level of the provided sequence.
  @param xs <sequence> The sequence to flatten.
*)
seq.flatten :: { xs: Sequence => 
  let xs = tosequence(xs) in 
  let ys = tosequence(seq.next(xs)) in
  seq.make { =>
    cases seq.next(ys)
      given unit =>
        do
          ys <- tosequence(seq.next(xs))
          let y = seq.next(ys) in
          do
            while y == unit and ys /= unit
              ys <- tosequence(seq.next(xs))
              y <- seq.next(ys)
            end

            y
          end
        end
      given y => y
    end
  }
}

(*
  Map a function over the elements of the provided sequence and concatenate
  the resulting sequences.
  @param xs <Sequence> The input sequence.
  @param func <Function> The transformation function that should return a 
                         sequence.
*)
seq.concatMap :: { xs: Sequence, func: Function =>
  xs
  |> tosequence(_)
  |> seq.map(_, func)
  |> seq.flatten(_)
}
